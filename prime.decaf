bool[] num;

void main()
{
    int i;
    num = init(ReadInteger());
    for (i = 2; i < num.length(); i = i + 1) {
        if (num[i]) {
            sieve(num, i);
        }
    }
    report(num);
}

bool[] init(int max)
{
    int i;
    bool[] res;
    res = NewArray(max, bool);
    res[0] = false;
    res[1] = false;
    for (i = 2; i < max; i = i + 1) {
        res[i] = true;
    }
    return res;
}

void sieve(bool[] n, int factor)
{
    int i;
    for (i = factor * 2; i < n.length(); i = i + factor) {
        n[i] = false;
    }
}

void report(bool[] n)
{
    int i;
    for (i = 0; i < n.length(); i = i + 1) {
        if (n[i])
            Print(i, " ");
    }
}
